// tag::doc[]
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    // ...
    // end::doc[]
    id 'io.jmix' version '2.4.0-RC1'
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.9'
    // tag::doc[]
    id 'org.openapi.generator' version '7.8.0'
}
// ...
// end::doc[]

apply plugin: 'org.springframework.boot'
apply plugin: 'com.vaadin'

jmix {
    bomVersion = '2.4.0-RC1'
}

group = 'com.company'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://nexus.jmix.io/repository/public'
    }
}

// tag::doc[]
dependencies {
    // ...
    // end::doc[]
    implementation 'io.jmix.core:jmix-core-starter'
    implementation 'io.jmix.data:jmix-eclipselink-starter'
    implementation 'io.jmix.security:jmix-security-starter'
    implementation 'io.jmix.security:jmix-security-flowui-starter'
    implementation 'io.jmix.security:jmix-security-data-starter'
    implementation 'io.jmix.localfs:jmix-localfs-starter'
    implementation 'io.jmix.flowui:jmix-flowui-starter'
    implementation 'io.jmix.flowui:jmix-flowui-data-starter'
    implementation 'io.jmix.flowui:jmix-flowui-themes'
    implementation 'io.jmix.datatools:jmix-datatools-starter'
    implementation 'io.jmix.datatools:jmix-datatools-flowui-starter'

    implementation 'org.springframework.boot:spring-boot-starter-web'

    runtimeOnly 'org.hsqldb:hsqldb'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.jmix.flowui:jmix-flowui-test-assist'

    // tag::doc[]
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'org.mapstruct:mapstruct:1.6.1'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.1'
}

sourceSets {
    main.java.srcDirs += "$buildDir/generated/openapi/petclinic/src/main/java"
}
// ...
// end::doc[]

configurations.implementation {
    exclude group: 'com.vaadin', module:'hilla'
    exclude group: 'com.vaadin', module:'hilla-dev'
}


test {
    useJUnitPlatform()
}

vaadin {
    optimizeBundle = false
}

idea {
    module {
        excludeDirs.add(file(".jmix"))
        excludeDirs.add(file("node_modules"))
        excludeDirs.add(file("src/main/frontend/generated"))
        excludeDirs.add(file("src/main/bundles"))
    }
}

// tag::doc[]
tasks.register('openApiGeneratePetclinic', GenerateTask) {
    inputSpec = "$rootDir/src/main/resources/petclinic-openapi.yml"
    outputDir = "$buildDir/generated/openapi/petclinic/"
    generatorName = "java"
    library = "restclient"
    packageName = "com.company.vettraining.petclinic"
    modelPackage = "com.company.vettraining.petclinic.model"
    apiPackage = "com.company.vettraining.petclinic.api"
    modelNameSuffix = "Model"
    generateApiTests = false
    generateModelTests = false
    skipValidateSpec = true
    configOptions.set([
            useRuntimeException: "true",
            useJakartaEe       : "true"
    ])
}
compileJava.dependsOn tasks.named("openApiGeneratePetclinic")
// end::doc[]
